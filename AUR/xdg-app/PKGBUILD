# Maintainer: Max Bruckner
_pkgbase=xdg-app
pkgname="${_pkgbase}-git"
pkgver=r162.d22f1d0
pkgrel=1
pkgdesc="Tool for managing sandboxed applications and their runtimes."
arch=( "i686" "x86_64" )
url="https://github.com/alexlarsson/xdg-app"
license=('unknown')
depends=( 'ostree')
makedepends=( 'git' )
provides=( 'xdg-app' )
conflicts=( 'xdg-app' )
source=( 
	"git+https://github.com/alexlarsson/${_pkgbase}.git"
	'g_strv_contains.c'
)
sha512sums=( 
	'SKIP'
	'9008c286b9888f998918b183f6c5769f6ad5416cd87e1bd5fc5e19c2bc5b76ec3847e14a714953b2cdd4b77cd5e708482dd94e99cd11ee867cf2a929a956597b'
)

pkgver() {
	cd "${srcdir}/${_pkgbase}"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${_pkgbase}"
	#Add g_strv_contains function because it is introduced in 
	#glib-2.44 which isn't currently used on Archlinux
	cp ../g_strv_contains.c ./
	sed -i "s/#include \"xdg-app-utils.h\"/#include \"xdg-app-utils.h\"\n#include \"g_strv_contains.c\"/g" xdg-app-builtins-build-finish.c

	./autogen.sh --prefix=/usr
}

build() {
	cd "${srcdir}/${_pkgbase}"
	make
}

package() {
	cd "${srcdir}/${_pkgbase}"
	make DESTDIR="$pkgdir" install
}
