pkgname=terasology-devbuild
pkgver=1391
_omega_ver=124
pkgrel=1
pkgdesc="Yet another high resolution game with blocks like Minecraft! (Last succesful development build)"
arch=('x86_64' 'i686')
license=('Apache')
url="http://terasology.org"
options=('!strip')
depends=('java-runtime' 'xorg-server-utils' 'openal')
makedepends=('unzip')
source=(
	"$pkgname"	
	"${pkgname}.desktop"
	"http://jenkins.movingblocks.net/job/DistroOmega/${_omega_ver}/artifact/distros/omega/build/distributions/TerasologyOmega.zip")
sha512sums=('9d2562e769aee38a09de315f9900754827ec2720400e10553f0cbf78c0834bf325220c42c249f17999bc764aa4a0c12aa7abe162d43ea5327672c2fa88fa2669'
            '9ecacc34ae0a17cfe1031f32ee4f25e4e840bed072445ac0a8ffc1b2a012a7b60fed739fcc2ceab8083293a31e7409406bc190c4295022df82815f48c5541d19'
            '5eff1de4127aca3fd89f0d370d689b61be6c5807d7331ebf9c270a1c920f203012f6f2dc3e6096abde22d3b23b2efb22049232e740c191eb17c8b46697f52516')

package() {
	cd "$srcdir"
    install -Dm 755 "${srcdir}/${pkgname}" "$pkgdir/usr/bin/${pkgname}"
    install -Dm 644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

	#cleanup
    rm "${srcdir}/${pkgname}" "${srcdir}/${pkgname}.desktop" "${srcdir}/TerasologyOmega.zip"

	#extract and install icons
	unzip -u libs/engine-* "org/terasology/icons/*"
	pushd org/terasology/icons
	for icon in *
	do
		size=${icon##*_}	#gooey_sweet_XX.png -> XX.png
		size=${size/.png/}	#XX.png -> XX
		install -Dm 644 "$icon" "${pkgdir}/usr/share/icons/${size}x${size}/apps/${pkgname}.png"
	done
	install -Dm 644 "${pkgdir}/usr/share/icons/64x64/apps/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	popd
	rm -r org

    #remove files/dirs for other operating systems
    rm run_macosx.command Terasology.{x86,x64}.exe
    rm -r natives/{macosx,windows}

    cp -ra "$srcdir" "${pkgdir}/usr/share/${pkgname}"
}
